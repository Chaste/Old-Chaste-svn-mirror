"""Copyright (c) 2005-2012, University of Oxford.
All rights reserved.

University of Oxford means the Chancellor, Masters and Scholars of the
University of Oxford, having an administrative office at Wellington
Square, Oxford OX1 2JD, UK.

This file is part of Chaste.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:
 * Redistributions of source code must retain the above copyright notice,
   this list of conditions and the following disclaimer.
 * Redistributions in binary form must reproduce the above copyright notice,
   this list of conditions and the following disclaimer in the documentation
   and/or other materials provided with the distribution.
 * Neither the name of the University of Oxford nor the names of its
   contributors may be used to endorse or promote products derived from this
   software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT
OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
"""

"""
Specialised component build script for python code.
"""

import os

Import("*")

curdir = os.getcwd()
# Look for source files within the <component> folder
os.chdir('../..') # This is so .o files are built in <component>/build/<something>/
pydirs = SConsTools.FindSourceFiles(env, '.', sourceExts=['.py'], ignoreDirs=['build', 'test'], dirsOnly=True)
# Move back to the buid dir
os.chdir(curdir)

# Look for files containing a test suite
# A list of test suites to run will be found in a test/<name>TestPack.txt
# file, one per line.
# Alternatively, a single test suite may have been specified on the command line.
testfiles = SConsTools.FindTestsToRun(env, build, BUILD_TARGETS,
                                      single_test_suite,
                                      single_test_suite_dir,
                                      all_tests,
                                      component='python')

# Test runner for Python tests needs to use a source scanner
# to find what Python files we depend on.
runtest = Builder(action=env['BUILDERS']['RunTest'].action,
                  source_scanner=SConsTools.PyScanner())
env['BUILDERS']['PyRunTest'] = runtest

# The list of paths to search for Python modules.
env.Append(PYINCPATH=pydirs)

test_log_files = []
if not compile_only:
    for testfile in testfiles:
        prefix = os.path.splitext(testfile)[0]
        log_file = env.File(prefix+'.log')
        test_log_files.append(log_file)
        env.PyRunTest(log_file, os.path.join('test', testfile))
        if force_test_runs:
            env.AlwaysBuild(log_file)
    
result = test_log_files
Return("result")
